-------------------------------------------------------------------------
-- Expr
-------------------------------------------------------------------------

scheme expr "Expr" =
  view E =
    holes [ node e: Expr, valGam: ValGam, tyGam: TyGam, kiGam: KiGam | ty: Ty | ]
    judgespec kiGam ; tyGam ; valGam :- e : ty
    judgeuse tex valGam :-.."e" e : ty
    explain (Within environment |valGam| , expression |e| has type |ty| .)
    explain ty = (Type of expression)
    explain e = (Expression)
    explain valGam = (Environment | (ident :-> ty)..._ | for value identifiers)
  view K =
    holes [ knTy: Ty | | retain ty: Ty ]
    judgespec kiGam ; tyGam; valGam; knTy :- e : ty
    judgeuse tex valGam; knTy :-.."e" e : ty
    explain (Within environment |valGam| , expecting the type of expression |e| to be |knTy| , |e| has type |ty| .)
    explain knTy = ("Expected/known" type of expression)
    explain tyGam = (Environment | (ident :-> ty)..._ | for type identifiers, cannot be modified (hence treated as a global constant in "\\ruleRef{e.ann}"))
  view C =
    holes [ | thread tyVarMp: VarMp | ]
    judgespec kiGam ; tyGam ; valGam ; tyVarMp.inh ; knTy :- e : ty ~> tyVarMp.syn
    judgeuse tex valGam ; tyVarMp.inh ; knTy :-.."e" e : ty ~> tyVarMp.syn
    explain (Within environment |valGam| , expecting the type of expression |e| to be |tyVarMp.inh knTy| , |e| has type |ty| , under constraints |tyVarMp.syn| .)
    explain tyVarMp.inh = (Already known constraints)
    explain tyVarMp.syn = (|tyVarMp.inh| + new constraints)
  view I1 =
    holes [ fiopt: FIOpts | | ]
    judgespec fiopt ; kiGam ; tyGam ; valGam ; tyVarMp.inh ; knTy :- e : ty ~> tyVarMp.syn
    judgeuse tex fiopt ; valGam ; tyVarMp.inh ; knTy :-.."e" e : ty ~> tyVarMp.syn
    explain (Within environment |valGam| and context |fiopt| , expecting the type of expression |e| to be |tyVarMp.inh knTy| , |e| has type |ty| , under constraints |tyVarMp.syn| .)
    explain fiopt = (|fitsIn| options, additional contextual information for | <= |)
  view I2 =
    holes [ | thread ityVarMp: IVarMp | retain ity: Ty ]
    judgespec fiopt ; kiGam ; tyGam ; valGam ; ityVarMp.inh ; tyVarMp.inh ; knTy :- e : ity ; ty ~> ityVarMp.syn ; tyVarMp.syn
    judgeuse tex fiopt ; valGam ; ityVarMp.inh ; tyVarMp.inh ; knTy :-.."e" e : ity ; ty ~> ityVarMp.syn ; tyVarMp.syn
    explain (Within environment |valGam| and context |fiopt| , expecting the types of expression |e| to be |ityVarMp.inh knTy| (and |tyVarMp.inh knTy|)
            , |e| has type |ity| (and |ty|) , under constraints |ityVarMp.syn| (and |tyVarMp.syn|) .
            )
    explain ityVarMp.inh = (Already known constraints (for quantifier propagation))
    explain ityVarMp.syn = (|ityVarMp.inh| + new constraints (for quantifier propagation))
    explain ity = (Type (with type alternatives |talt|) of expression (for quantifier propagation))
  view CG =
    holes [ | | retain translExpr: TranslExpr ]
    judgespec fiopt ; kiGam ; tyGam ; valGam ; tyVarMp.inh ; knTy :- e : ty ~> tyVarMp.syn ; translExpr
    judgeuse tex fiopt ; valGam ; tyVarMp.inh ; knTy :-.."e" e : ty ~> tyVarMp.syn ; translExpr
    explain translExpr = (Translated expression)


-------------------------------------------------------------------------
-- Declaration
-------------------------------------------------------------------------

scheme decl "Decl" =
  view E =
    holes [ kiGam: KiGam, tyGam: TyGam, valGam: ValGam, node d: Decl | | gathTySigGam: ValGam ]
    judgespec kiGam ; tyGam ; valGam :-.."d" d : gathTySigGam
    judgeuse tex valGam :-.."d" d : gathTySigGam
    explain (Within environment |valGam| , declaration |d| has type signature bindings |gathTySigGam| .)
    explain valGam = (Environment with known bindings)
    explain gathTySigGam = (Environment with type signature bindings)
    explain d = (Declaration)
  view K =
    holes [ tySigGam: ValGam | thread patValGam: ValGam | ]
    judgespec tySigGam ; patValGam.inh ; kiGam ; tyGam ; valGam :- d : gathTySigGam ; patValGam.syn
    judgeuse tex tySigGam ; patValGam.inh ; valGam :-.."d" d : gathTySigGam ; patValGam.syn
    explain (Declaration |d| has explicit type bindings |gathTySigGam| ,
             within explicit bindings |tySigGam| and implicit type bindings |patValGam.inh| ,
             and type checks within |valGam| ,
             yielding additional bindings |patValGam.syn| .
            )
    explain gathTySigGam = (Environment with new type signature bindings)
    explain tySigGam = (Collected |gathTySigGam| , used by patterns to extract bindings for pattern variables)
    explain patValGam.inh = ("Known/gathered" pattern variable bindings)
    explain patValGam.syn = (|patValGam.inh| + new bindings)
    explain tyGam = (Environment | (ident :-> ty)..._ | for type identifiers, cannot be modified (hence treated as a global constant in "\\ruleRef{e.ann}"))
  view C =
    holes [ | thread tyVarMp: VarMp, thread patTyVarMp: VarMp | ]
    judgespec tySigGam ; patValGam.inh ; kiGam ; tyGam ; valGam ; patTyVarMp.inh ; tyVarMp.inh :- d : gathTySigGam ; patValGam.syn ~> patTyVarMp.syn ; tyVarMp.syn
    judgeuse tex tySigGam ; patValGam.inh ; valGam ; patTyVarMp.inh ; tyVarMp.inh :-.."d" d : gathTySigGam ; patValGam.syn ~> patTyVarMp.syn ; tyVarMp.syn
    explain (Declaration |d| has explicit type bindings |gathTySigGam| ,
             within explicit bindings |tySigGam| and implicit type bindings |patTyVarMp.inh patValGam.inh| ,
             and type checks within |tyVarMp.inh valGam| ,
             yielding additional bindings |patValGam.syn| ,
             under constraints |patTyVarMp.syn| (for |patValGam.syn|) and
             |tyVarMp.syn| (for |valGam|).
            )
    explain patTyVarMp.inh = ("Known/gathered" constraints during type inference of patterns (i.e. use of type signatures and pattern structure))
    explain patTyVarMp.syn = (|patTyVarMp.inh| + new constraints)
    explain tyVarMp.inh = ("Known/gathered" constraints during type inference of expressions bound to patterns)
    explain tyVarMp.syn = (|tyVarMp.inh| + new constraints)
  view I2 =
    holes [ | thread ityVarMp: IVarMp, thread tySigTyVarMp: VarMp | ]
    judgespec tySigGam ; patValGam.inh ; kiGam ; tyGam ; valGam ; tySigTyVarMp.inh ; patTyVarMp.inh ; ityVarMp.inh ; tyVarMp.inh :- d : gathTySigGam ; patValGam.syn ~> tySigTyVarMp.syn ; patTyVarMp.syn ; ityVarMp.syn ; tyVarMp.syn
    judgeuse tex tySigGam ; patValGam.inh ; valGam ; tySigTyVarMp.inh ; patTyVarMp.inh ; ityVarMp.inh ; tyVarMp.inh :-.."d" d : gathTySigGam ; patValGam.syn ~> tySigTyVarMp.syn ; patTyVarMp.syn ; ityVarMp.syn ; tyVarMp.syn
    explain tySigTyVarMp.inh = (Type signature information represented as constraint for binding to type variable in |gathTySigGam|)
    explain tySigTyVarMp.syn = (|tySigTyVarMp.inh| + new constraints)
    explain ityVarMp.inh = ("Known/gathered" constraints during quantifier propagation)
    explain ityVarMp.syn = (|ityVarMp.inh| + new constraints)
  view CG =
    holes [ | | translBind: TransDecl ]
    judgespec tySigGam ; patValGam.inh ; kiGam ; tyGam ; valGam ; patTyVarMp.inh ; tyVarMp.inh :- d : gathTySigGam ; patValGam.syn ~> patTyVarMp.syn ; tyVarMp.syn ; translBind
    judgeuse tex tySigGam ; patValGam.inh ; valGam ; patTyVarMp.inh ; tyVarMp.inh :-.."d" d : gathTySigGam ; patValGam.syn ~> patTyVarMp.syn ; tyVarMp.syn ; translBind



